// Copyright (c) 2025 WSO2 LLC. (http://www.wso2.com).
//
// WSO2 LLC. licenses this file to you under the Apache License,
// Version 2.0 (the "License"); you may not use this file except
// in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.

import ballerina/jballerina.java;

# Represents a Solace listener endpoint that can be used to receive messages from a Solace topic or a queue.
public isolated class Listener {

    # Initializes a new Solace message listener with the given broker URL and configuration.
    # ```ballerina
    # listener jms:Listener messageListener = check new (
    #     url = "smf://localhost:55554",
    #     messageVpn = "default",
    #     auth = {
    #         username: "admin",
    #         password: "admin"
    #     }
    # );
    # ```
    #
    # + url - The Solace broker URL in the format `<scheme>://[username]:[password]@<host>[:port]`.
    # Supported schemes are `smf` (plain-text) and `smfs` (TLS/SSL).
    # Multiple hosts can be specified as a comma-separated list for failover support.
    # Default ports: 55555 (standard), 55003 (compression), 55443 (SSL)
    # + config - configurations used when initializing the listener
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function init(string url, *ListenerConfiguration config) returns Error? {
        Error? validated = validateConfigurations(config);
        if validated is Error {
            return error Error(
                string `Error occurred while validating the listener configurations: ${validated.message()}`, validated);
        }
        return self.initListener(url, config);
    }

    isolated function initListener(string url, ListenerConfiguration config) returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener",
        name: "init"
    } external;

    # Attaches a Solace service to the listener.
    # ```ballerina
    # check messageListener.attach(solaceSvc);
    # ```
    #
    # + 'service - service instance
    # + name - service name
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function attach(Service 'service, string[]|string? name = ()) returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener"
    } external;

    # Detaches a Solace service from the listener.
    # ```ballerina
    # check messageListener.detach(solaceSvc);
    # ```
    #
    # + 'service - service instance
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function detach(Service 'service) returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener"
    } external;

    # Starts the listener.
    # ```ballerina
    # check messageListener.'start();
    # ```
    #
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function 'start() returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener"
    } external;

    # Gracefully stops the listener.
    # ```ballerina
    # check messageListener.gracefulStop();
    # ```
    #
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function gracefulStop() returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener"
    } external;

    # Immediately stops the listener.
    # ```ballerina
    # check messageListener.immediateStop();
    # ```
    #
    # + return - `jms:Error` if an error occurs or `()` otherwise
    public isolated function immediateStop() returns Error? = @java:Method {
        'class: "io.ballerina.lib.solace.jms.listener.Listener"
    } external;
}
